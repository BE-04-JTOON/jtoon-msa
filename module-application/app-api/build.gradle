plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

configurations {
    asciidoctorExtensions
}

ext {
    snippetsDir = file('build/generated-snippets')      // 스니펫이 생성되는 폴더 설정
}

test {
    outputs.dir snippetsDir          // 스니펫을 'snippetsDir'로 생성하도록 test task 설정
}

asciidoctor {
    configurations 'asciidoctorExtensions' // 'Asciidoctor' 확장에 대한 설정
    inputs.dir snippetsDir          // 불러올 스니펫 위치를 'snippetsDir'로 설정
    dependsOn test                  // test task 이후에 'asciidoctor'를 실행하도록 설정
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

bootJar {
    dependsOn copyDocument
}

build {
    dependsOn copyDocument
}

dependencies {
    // Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Bean Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // AOP
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // H2
    implementation 'com.h2database:h2'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'
    
    // OAuth2
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Asciidoctor
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // RestDocs
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}
